apply plugin: "com.android.application"
apply plugin: "com.google.firebase.firebase-crash"

def supportVersion = "25.3.1"
def firebaseVersion = "10.2.6"
def productVersion = "1.2.0"
def butterKnifeVersion = "8.5.1"
def okHttpVersion = "3.6.0"

android {
    compileSdkVersion 25
    buildToolsVersion "26.0.0-rc2"
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        release {
            storeFile file("../release.keystore")
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
        if(file("../project.properties").exists()) {
            Properties props = new Properties()
            props.load(new FileInputStream(file("../project.properties")))

            debug {
                storeFile file("../debug_keystore.jks")
                storePassword props['keyPassword']
                keyAlias props['keyAlias']
                keyPassword props['keyPassword']
            }
        }
    }

    defaultConfig {
        applicationId "im.bernier.petfinder"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 9000
        versionName "${productVersion}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            zipAlignEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            debuggable true
        }
    }

    flavorDimensions "store"

    productFlavors {
        dev {
            dimension "store"
            minSdkVersion 24
        }
        prod {
            dimension "store"
            versionCode 'git rev-list HEAD --count --no-merges'.execute().text.trim().toInteger()
        }
    }

    lintOptions {
        disable "InvalidPackage"
    }
}

dependencies {
    compile "com.android.support:cardview-v7:${supportVersion}"
    compile "com.android.support:recyclerview-v7:${supportVersion}"
    compile "com.android.support:appcompat-v7:${supportVersion}"
    compile "com.android.support:design:${supportVersion}"

    compile "com.google.android.gms:play-services-places:${firebaseVersion}"
    compile "com.google.firebase:firebase-core:${firebaseVersion}"
    releaseCompile "com.google.firebase:firebase-crash:${firebaseVersion}"

    compile "com.squareup.retrofit2:retrofit:2.1.0"
    compile "com.squareup.okhttp3:okhttp:${okHttpVersion}"
    compile "com.squareup.okhttp3:logging-interceptor:${okHttpVersion}"

    compile "com.jakewharton.timber:timber:4.5.1"

    compile "com.jakewharton:butterknife:${butterKnifeVersion}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${butterKnifeVersion}"

    compile "com.squareup.picasso:picasso:2.6.0-SNAPSHOT"

    compile "com.github.chrisbanes:PhotoView:1.3.1"

    compile('com.mikepenz:aboutlibraries:5.9.1@aar') {
        transitive = true
    }

    compile ("com.squareup.retrofit2:converter-simplexml:2.1.0", {
        exclude module: "stax"
        exclude module: "stax-api"
        exclude module: "xpp3"
    })

    testCompile "junit:junit:4.12"
    androidTestCompile("com.android.support.test.espresso:espresso-core:2.2.2", {
        exclude group: "com.android.support", module: "support-annotations"
    })
}

apply plugin: "com.google.gms.google-services"
