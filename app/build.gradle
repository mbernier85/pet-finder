apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "com.google.firebase.firebase-crash"

def supportVersion = "26.0.0-beta2"
def firebaseVersion = "11.0.2"
def productVersion = "1.2.0"
def butterKnifeVersion = "8.7.0"
def okHttpVersion = "3.8.1"

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        release {
            storeFile file("../release.keystore")
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
        if(file("../project.properties").exists()) {
            Properties props = new Properties()
            props.load(new FileInputStream(file("../project.properties")))

            debug {
                storeFile file("../debug_keystore.jks")
                storePassword props["keyPassword"]
                keyAlias props["keyAlias"]
                keyPassword props["keyPassword"]
            }
        } else {
            println("missing project.properties")
        }
    }

    defaultConfig {
        applicationId "im.bernier.petfinder"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 9000
        versionName "${productVersion}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            zipAlignEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            debuggable true
        }
    }

    flavorDimensions "store"

    productFlavors {
        dev {
            dimension "store"
        }
        prod {
            dimension "store"
            versionCode "git rev-list HEAD --count --no-merges".execute().text.trim().toInteger()
        }
    }

    lintOptions {
        disable "InvalidPackage"
    }
}

dependencies {
    implementation "com.android.support:cardview-v7:${supportVersion}"
    implementation "com.android.support:recyclerview-v7:${supportVersion}"
    implementation "com.android.support:appcompat-v7:${supportVersion}"
    implementation "com.android.support:design:${supportVersion}"

    implementation "com.google.android.gms:play-services-places:${firebaseVersion}"
    implementation "com.google.firebase:firebase-core:${firebaseVersion}"
    releaseImplementation "com.google.firebase:firebase-crash:${firebaseVersion}"

    implementation "com.squareup.retrofit2:retrofit:2.3.0"
    implementation "com.squareup.okhttp3:okhttp:${okHttpVersion}"
    implementation "com.squareup.okhttp3:logging-interceptor:${okHttpVersion}"

    implementation "com.jakewharton.timber:timber:4.5.1"

    implementation "com.jakewharton:butterknife:${butterKnifeVersion}"
    kapt "com.jakewharton:butterknife-compiler:${butterKnifeVersion}"

    implementation "com.squareup.picasso:picasso:2.6.0-SNAPSHOT"

    implementation "com.github.chrisbanes:PhotoView:1.3.1"

    implementation("com.mikepenz:aboutlibraries:5.9.1@aar") {
        transitive = true
    }

    implementation ("com.squareup.retrofit2:converter-simplexml:2.1.0", {
        exclude module: "stax"
        exclude module: "stax-api"
        exclude module: "xpp3"
    })

    testImplementation "junit:junit:4.12"
    androidTestImplementation("com.android.support.test.espresso:espresso-core:2.2.2", {
        exclude group: "com.android.support", module: "support-annotations"
    })
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
}

apply plugin: "com.google.gms.google-services"